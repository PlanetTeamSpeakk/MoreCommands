plugins {
    id "com.github.johnrengelman.shadow" version "7.1.2"
}

architectury {
    platformSetupLoomIde()
    fabric()
}

loom {
    accessWidenerPath = project(":common").loom.accessWidenerPath

    splitEnvironmentSourceSets()

    mods {
        morecommands {
            sourceSet sourceSets.main
            sourceSet sourceSets.client
        }
    }
}

configurations {
    common
    shadowCommon // Don't use shadow from the shadow plugin because we don't want IDEA to index this.
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentFabric.extendsFrom common
}

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    minecraft "com.mojang:minecraft:${rootProject.minecraft_version}"
    mappings loom.officialMojangMappings()

    modImplementation "net.fabricmc:fabric-loader:${rootProject.fabric_loader_version}"
    modApi "net.fabricmc.fabric-api:fabric-api:${rootProject.fabric_api_version}"
    modApi "dev.architectury:architectury-fabric:${rootProject.architectury_version}"
    modApi "me.lucko:fabric-permissions-api:0.1-SNAPSHOT"
    modImplementation "com.terraformersmc:modmenu:4.0.6"
    modImplementation "com.ptsmods:devlogin:3.2.2"

    clientImplementation project(":common").sourceSets.client.output
    common(project(path: ":common", configuration: "namedElements")) { transitive false }
    shadowCommon(project(path: ":common", configuration: "transformProductionFabric")) { transitive false }
    common(project(path: ":common:API", configuration: "namedElements")) { transitive false }
    shadowCommon(project(path: ":common:API", configuration: "transformProductionFabric")) { transitive false }

    project(":common:Compat").subprojects.collect { if (it.parent == project(":common:Compat"))
        common(project(path: ":common:Compat:" + it.name, configuration: "namedElements")) { transitive false } else null }
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

shadowJar {
    destinationDirectory.set file(destinationDirectory.get().asFile.toPath().resolve("../devlibs"))
    exclude "architectury.common.json"

    configurations = [project.configurations.shadowCommon]
    classifier "dev-shadow"
}

task shadowCompat(type: Jar) {
    archiveFileName = "${archivesBaseName}-${archiveVersion.get()}_${rootProject.minecraft_version_least}-${rootProject.minecraft_version}_FABRIC.${archiveExtension.get()}"
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
    manifest.from jar.manifest
    from zipTree(remapJar.archiveFile)
    from { project(":common:Compat").subprojects.findAll { ("forge" != it.name) }.collect { zipTree(it.remapJar.archiveFile) } }
}

tasks.build.dependsOn(shadowCompat)

remapJar {
    destinationDirectory.set file(destinationDirectory.get().asFile.toPath().resolve("../devlibs"))
    injectAccessWidener = true
    inputFile.set shadowJar.archiveFile
    dependsOn shadowJar
    classifier "dev-shadow-remap"
}

jar {
    destinationDirectory.set file(destinationDirectory.get().asFile.toPath().resolve("../devlibs"))
    classifier "dev"
}

sourcesJar {
    def sources = project(":common").subprojects.collect { it.sourcesJar }
    sources.add project(":common").sourcesJar
    dependsOn sources

    from { sources.collect { zipTree(it.archiveFile) } }
}

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}
