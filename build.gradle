buildscript {
	dependencies {
		classpath 'com.github.jengelman.gradle.plugins:shadow:6.0.0'
	}
}

plugins {
	id 'fabric-loom' version '0.4-SNAPSHOT'
}

apply plugin: 'com.github.johnrengelman.shadow'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

task fatJar(type: com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar) {
	archiveFileName = "${archivesBaseName}-${archiveVersion.get()}_${project.minecraft_version}.${archiveExtension.get()}"
	from remapJar
	configurations = [project.configurations.compile]
	exclude("/mappings/") // Included within project.configurations.compile for some reason.
	exclude("/com/sun/") // This dependency is shadowed within the DRPC library, but it already exists on Minecraft's classpath by default, so to save space, it is removed.
	exclude("/META-INF/maven/") // Unnecessary
}

shadowJar.dependsOn remapJar
tasks.build.dependsOn fatJar

repositories {
	mavenCentral()
	maven { url 'https://jitpack.io' }
}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
	compile "com.github.Vatuu:discord-rpc:1.6.2"
}

processResources {
	inputs.property "version", project.version

	from(sourceSets.main.resources.srcDirs) {
		include "fabric.mod.json"
		expand "version": project.version
	}

	from(sourceSets.main.resources.srcDirs) {
		exclude "fabric.mod.json"
	}
}

tasks.withType(JavaCompile) {
	options.encoding = "UTF-8"
}

// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
// if it is present.
// If you remove this task, sources will not be generated.
task sourcesJar(type: Jar, dependsOn: classes) {
	archiveClassifier.set("sources")
	from sourceSets.main.allSource
}
