plugins {
    id "architectury-plugin" version "3.4-SNAPSHOT"
    id "dev.architectury.loom" version "1.2-SNAPSHOT" apply false
    id "io.freefair.lombok" version "6.4.3" apply false
    id "io.github.juuxel.loom-quiltflower" version "1.7.1" apply false
    id "io.github.pacifistmc.forgix" version "1.2.6"
}

architectury {
    minecraft = rootProject.minecraft_version
}

subprojects {
    apply plugin: "dev.architectury.loom"
    apply plugin: "io.freefair.lombok"
    apply plugin: "io.github.juuxel.loom-quiltflower"
}

allprojects {
    apply plugin: "java"
    apply plugin: "architectury-plugin"
    apply plugin: "maven-publish"

    sourceCompatibility = JavaVersion.VERSION_16
    targetCompatibility = JavaVersion.VERSION_16

    archivesBaseName = rootProject.archives_base_name
    version = rootProject.mod_version
    group = rootProject.maven_group

    repositories {
        mavenLocal()
        maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
        maven { url 'https://maven.terraformersmc.com/releases/' }
    }

    configurations.all {
        resolutionStrategy {
            force("net.fabricmc:fabric-loader:$fabric_loader_version")
        }
    }

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
    }

    java {
        withSourcesJar()
    }
}

forgix {
    group = "com.ptsmods.morecommands"
    outputDir = "build/libs"
    mergedJarName = "${archivesBaseName}-${project.version}_${project.minecraft_version_least}-${project.minecraft_version}.jar"

    fabric {
        jarLocation = "build/libs/${archivesBaseName}-${project.version}_${project.minecraft_version_least}-${project.minecraft_version}_FABRIC.jar"
    }

    forge {
        jarLocation = "build/libs/${archivesBaseName}-${project.version}_${project.minecraft_version_least}-${project.minecraft_version}_FORGE.jar"
    }
}

tasks.register('doMerge') {
    dependsOn(":fabric:build", ":forge:build")
    finalizedBy(mergeJars)
}

tasks.build.dependsOn(tasks.doMerge)
