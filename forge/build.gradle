plugins {
    id "com.github.johnrengelman.shadow" version "7.1.2"
}

architectury {
    platformSetupLoomIde()
    forge()
}

loom {
    accessWidenerPath = project(":common").loom.accessWidenerPath

    forge {
        convertAccessWideners = true
        extraAccessWideners.add loom.accessWidenerPath.get().asFile.name

        mixinConfig "morecommands_client.mixins.json"
        mixinConfig "morecommands_common.mixins.json"
        mixinConfig "morecommands_reach.mixins.json"
        mixinConfig "morecommands_compat16.mixins.json"
        mixinConfig "morecommands_compat17.mixins.json"
        mixinConfig "morecommands_compat18.mixins.json"
        mixinConfig "morecommands_compat182.mixins.json"
        mixinConfig "morecommands_compat19.mixins.json"
        mixinConfig "morecommands_compat190.mixins.json"
        mixinConfig "morecommands_compat191.mixins.json"
        mixinConfig "morecommands_compat192.mixins.json"
    }
}

configurations {
    common
    shadowCommon // Don't use shadow from the shadow plugin because we don't want IDEA to index this.
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentForge.extendsFrom common
}

dependencies {
    minecraft "com.mojang:minecraft:${rootProject.minecraft_version}"
    mappings loom.officialMojangMappings()

    forge "net.minecraftforge:forge:${rootProject.forge_version}"
    modApi "dev.architectury:architectury-forge:${rootProject.architectury_version}"
    modImplementation "com.ptsmods:devlogin:3.1"

    common 'com.ptsmods:mysqlw:1.9.2'
    shadowCommon "com.ptsmods:mysqlw:1.9.2"
    common(project(path: ":common", configuration: "namedElements")) { transitive false }
    shadowCommon(project(path: ":common", configuration: "transformProductionFabric")) { transitive false }
    common(project(path: ":common:API", configuration: "namedElements")) { transitive false }
    shadowCommon(project(path: ":common:API", configuration: "transformProductionFabric")) { transitive false }

    project(":common:Compat").subprojects.collect { common(project(path: ":common:Compat:" + it.name,
            configuration: "namedElements")) { transitive false } }
}

processResources {
    inputs.property "version", project.version

    filesMatching("META-INF/mods.toml") {
        expand "version": project.version
    }
}

shadowJar {
    destinationDirectory.set file(destinationDirectory.get().asFile.toPath().resolve("../devlibs"))
    exclude "fabric.mod.json"
    exclude "architectury.common.json"

    configurations = [project.configurations.shadowCommon]
    classifier "dev-shadow"
}

task shadowCompat(type: Jar) {
    archiveFileName = "${archivesBaseName}-${archiveVersion.get()}_${rootProject.minecraft_version_least}-${rootProject.minecraft_version}_FORGE.${archiveExtension.get()}"
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
    from zipTree(remapJar.archiveFile)
    from { project(":common:Compat").subprojects.collect { zipTree(it.transformProductionForge.archiveFile) } }
}

tasks.build.dependsOn(shadowCompat)

remapJar {
    destinationDirectory.set file(destinationDirectory.get().asFile.toPath().resolve("../devlibs"))
    inputFile.set shadowJar.archiveFile
    dependsOn shadowJar
    classifier "dev-shadow-remap"
}

jar {
    destinationDirectory.set file(destinationDirectory.get().asFile.toPath().resolve("../devlibs"))
    classifier "dev"
}

sourcesJar {
    def sources = project(":common").subprojects.collect { it.sourcesJar }
    sources.add project(":common").sourcesJar
    dependsOn sources

    from { sources.collect { zipTree(it.archiveFile) } }
}

repositories {
    mavenCentral()
}

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}
